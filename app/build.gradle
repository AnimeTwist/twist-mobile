apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'

// load our secrets
def secretProperties = new Properties()
if ("$System.env.CI" != "true") {
    def secretsFile = rootProject.file("secrets.properties")
    secretProperties.load(new FileInputStream(secretsFile))
} else {
    secretProperties.setProperty("decrypt_key", "$System.env.DECRYPT_KEY")
}


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "dev.smoketrees.twist"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 10
        versionName "2.2-prod7"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        buildConfigField("byte[]", "CRYPTO_KEY", byteArrayLiteral(secretProperties["decrypt_key"]))

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.incremental":"true"
                ]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            multiDexEnabled = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // For Kotlin projects
    kotlinOptions {
        jvmTarget = "1.8"
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation "androidx.fragment:fragment-ktx:1.2.0-rc05"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0-rc03"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0-rc03"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-rc03"

    // Navigation component
    implementation "androidx.navigation:navigation-fragment-ktx:2.1.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.1.0"

    // Paging support
    implementation "androidx.paging:paging-runtime-ktx:2.1.1"

    // Koin for DI
    implementation "org.koin:koin-android-viewmodel:2.0.1"

    // Networking stuff
    implementation "com.google.code.gson:gson:2.8.5"
    implementation "com.squareup.retrofit2:retrofit:2.6.2"
    implementation "com.squareup.retrofit2:converter-gson:2.6.2"
    implementation "com.squareup.okhttp3:logging-interceptor:4.2.2"

    // UI
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.recyclerview:recyclerview:1.2.0-alpha01"
    implementation "com.google.android.material:material:1.0.0"
    implementation "com.afollestad.material-dialogs:core:3.1.1"

    // apache commons
    implementation "org.apache.commons:commons-lang3:3.9"

    // Exoplayer - stream video
    implementation 'com.google.android.exoplayer:exoplayer:2.10.5'

    // Spinkit - loading animations
    implementation 'com.github.ybq:Android-SpinKit:1.4.0'

    // Room for database stuff
    implementation "androidx.room:room-runtime:2.2.3"
    kapt "androidx.room:room-compiler:2.2.3"

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:2.2.3"

    // glide for image loading
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    kapt 'com.github.bumptech.glide:compiler:4.10.0'

    // multidex
    implementation 'com.android.support:multidex:1.0.3'

    // android system permissions
    implementation 'com.github.quickpermissions:quickpermissions-kotlin:0.4.0'

    // html rendering
    implementation "org.sufficientlysecure:html-textview:3.9"

}

def byteArrayLiteral(String string) {
    return "new byte[] { " + string.bytes.join(", ") + "}"
}